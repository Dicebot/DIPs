# Dependency-Carrying Declarations

| Field           | Value                                                      |
|-----------------|------------------------------------------------------------|
| DIP:            | 1005                                                       |
| Author:         | Andrei Alexandrescu (andrei@erdani.com)                    |
| Implementation: | n/a                                                        |
| Status:         | Draft                                                      |

## Abstract

A Dependency-Carrying Declaration is a D declaration that does not require any `import` directive to be present outside of it. Such declarations encapsulate their own dependencies, which makes dependency relationships more fine-grained than traditional module- and package-level dependencies.

This DIP makes the argument that Dependency-Carrying Declarations are an essential mechanism for building scalable libraries and applications. It proposes a language addition called "inline import", which allows any D declaration to be transformed into a Dependency-Carrying Declaration.

### Links

n/a

## Description

forthcoming

### Rationale

Consider the following D code:

```d
import std.datetime;
import std.stdio;
void log(string message)
{
    writeln(Clock.currTime, ' ', message);
}
```

Traditionally (though not required by the language), `import`s are placed at the top of the module and then implicitly used by the declarations in the module. This has two consequences. First, the setup establishes a dependency of the current module on two other modules or packages (and by transitivity, on the transitive closure of the modules/packages those depend on). Second, it defines a _relationship at distance_ between the `log` function and the `import`s at the top. As a immediate practical consequence, `log` cannot be moved across the codebase without ensuring the appropriate `import` declarations are present in the target module.

Let us compare and contrast the setup above with the following:

```d
void log(string message)
{
    import std.datetime;
    import std.stdio;
    writeln(Clock.currTime, ' ', message);
}
```

This layout still preserves the dependency of the current module on the two `std` entities because the compiler would need them in order to compile `log`. However, the relationship at distance disappears---`log` encapsulates its dependencies, which migrate together with it. We call such a declaration that does not depend on `import`s outside of it, a _Dependency-Carrying Declaration_.

Consider now the case when `log` is a generic function:

```d
void log(T)(T message)
{
    import std.datetime;
    import std.stdio;
    writeln(Clock.currTime, ' ', message);
}
```

In this case, the current module depends on `std.datetime` and `std.stdio` only if it uses `log` directly from within a non-template function. Otherwise, the `log` generic function is only parsed to an AST (no symbol lookup) and not processed further. Should another module import this module and use `log`, the dependency is realized because `log` needs to be compiled. This makes the module that actually uses `log`---and only it---dependent on `std.datetime` and `std.stdio`, in addition of course to the module that defines `log`.

The same reasoning applies to template `struct`, `class`, or `interface` definitions:

```d
struct FileBuffer(Range)
{
    import std.stdio;
    private File output;
    ...
}
```

Such an entity only realizes the dependencies when actually instantiated, therefore moving the carried dependencies to the point of instantiation.

The analysis above reveals that Dependency-Carrying Declarations have multiple benefits:

* Specifies dependencies at declaration level, not at module level. This allows reasoning about the dependency cost of declarations in separation instead of aggregated at module level.
* Dependency-Carrying Declarations are easier to move around, making for simpler and faster refactorings.
* Dependency-Carrying Declarations allow scalable template libraries. Large libraries (such as D's standard library itself) are customarily distributed in packages and modules grouped by functional areas, such that client code can use the library without needing to `import` many dozens of small modules, each for one specific declaration. Conversely, client code often `import`s a package or module to use just a small fraction of it. Distributing a template library in the form of Dependency-Carrying Declarations creates a scalable, pay-as-you-go setup: The upfront cost of `import`ing such a module is only that of parsing the module source, which can reasonably be considered negligible in the economy of any build. Then, dependencies are pulled on a need basis depending on the declarations used by client code.

Dependency-Carrying Declarations also have drawbacks:

* If most declarations in a module need the same `import`s, then factoring them outside the declarations at top level is simpler and better than repeating them.
* Traditional dependency-tracking tools such as `make` and other build systems assume file-level dependencies and need special tooling (such as `rdmd`) in order to work efficiently.
* Dependencies at the top of a module are easier to inspect quickly than dependencies spread through the module.

On the whole, experience with using Dependency-Carrying Declarations in the D standard library suggests that the advantages outweigh disadvantages considerably. Using local `import`s is considered good style in D code.

#### Inline `import`s

There are declarations that cannot be reformulated in Dependency-Carrying Declaration form. Consider a simple example of a non-template function declaration:

```d
import std.stdio;
void process(File input);
```

It is not possible to declare `process` without `import`ing `std.stdio` outside of it. Another situation is that of template constraints:

```d
import std.range;
struct Buffered(Range) if (isInputRange!Range)
{
    ...
}
```

Again, there is no way to declare `Buffered` unless `isInputRange` is visible in the scope outside it, which in turn requires `std.range` to be imported.

This, combined with the ubquitous use of static introspection and constrained templates, has led to an unpleasant situation in the D standard library whereby it is practically impossible to eliminate `import`s at the top level. To date, in spite of an effort to place `import`s locally, the dependency structure of the D standard library has not improved visibly because of this limitation.

We propose an addition to the D language that allows the use of the keyword `import` as a pseudo-package name at the top level. When that syntax is used, it instructs the compiler to look up the symbol in the package/module that follows. To clarify by means of examples, the previous examples can be rewritten as:

```d
void process(import.std.stdio.File input);
struct Buffered(Range) if (import.std.range.isInputRange!Range)
{
    ...
}
```

With this syntax, the `import` is effected only if the declared name is actually looked up, and only looks up the imported package/module for that particular symbol only. Of course, simple caching will make several imports of the same module as expensive as the first.

The feature can be applied to any symbol that can be looked up in another module by means of existing `import` declarations. The usual lookup rules apply, for example either `import.std.range.isInputRange` or the more precise `import.std.range.primitives.isInputRange` may be used.

#### Alternatives Considered

There are a number of alternative approaches that have been (and some still are) considered.

* Full lazy `import`s. Assume all `import`s are lazy without any change in the language. That would allow an idiom by which libraries use fully qualified names everywhere, and the `import`s would be effected only when names are looked up. This would allow scalability but not the dependency-carrying aspect.
* Add syntax to allow for an optional `import` directive inside declarations:

```d
    void process(import std.stdio)(File input);
    struct Buffered(import std.range)(Range) if (isInputRange!Range)
    {
        ...
    }
```
This has the advantage of being less verbose in case the same module is looked up several times. The disadvantages are a heavier and more ambiguous syntax (two sets of parens for nontemplates, three for templates) and an unclear relationship between the `import`ed entities and the symbols used in the declaration.
* Use `import` with a syntax similar to `if` in the declaration:

```d
        void process(File input) import (std.stdio);
        struct Buffered(import std.range)(Range) if (isInputRange!Range)
        import (std.stdio)
        {
            ...
        }
```
Such an option is viable and has again the advantage of reducing repetition in case the same `import` is used multiple times. In addition, the `import` may be made available inside the definition of the function/data structure, when present.

### Breaking changes / deprecation process

We do not anticipate any breaking changes brought by this language addition.

## Copyright & License

Copyright (c) 2016 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

### Reviews
