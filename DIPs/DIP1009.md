# Improve Contract Syntax

| Field             | Value                                                           |
|-------------------|-----------------------------------------------------------------|
| DIP:              | 1009                                                            |
| Review Count:     | 2 [Most Recent]                                                 |
| Author:           | Zach Tollen(reachzach@gmail.com)                                |
| Implementation:   | [Timon Gehr]                                                    |
| Status:           | Formal Review                                                   |

[Most Recent]: https://github.com/dlang/DIPs/blob/5ac6323098035773fe8ca18362832e0162344b7f/DIPs/DIP1009.md

[Timon Gehr]: https://github.com/dlang/dmd/compare/master...tgehr:contract-syntax

## Abstract

D has already made a significant commitment to the theory of [Contract Programming](https://dlang.org/spec/contracts.html), by means of its existing `in`, `out`, and `invariant` constructs. But limitations remain to their usability, both in their syntax and in their implementation. This DIP addresses only the syntax aspect of those limitations, proposing a syntax which makes `in`, `out`, and `invariant` contracts much easier to read and write.

### Links
[DIP1003 discussion thread](http://forum.dlang.org/post/cklhgfbnpajbeefmwjrf@forum.dlang.org), which was the impetus for this DIP.

## Rationale

D's existing `in` and `out` contract syntaxes are hard to use for several reasons:

1. One must create a block statement for any contract. Because the largest share of contracts will be one line long, in order to keep this manageable for codebases that use them, a strict formatting style must be used. For example, the [SDC](https://github.com/SDC-Developers/SDC/) codebase writes contracts [like this:](https://github.com/SDC-Developers/SDC/blob/master/src/d/parser/expression.d#L1059)
```d
ulong strToDecInt(string s) in {
    assert(s.length > 0, "s must not be empty");
} body {
    ulong ret = 0;
    ...
}  
```
The same contract would occupy significantly more vertical space in other codebases (such as dmd or Phobos):
```d
ulong strToDecInt(string s)
in
{
    assert(s.length > 0, "s must not be empty");
}
body
{
    ulong ret = 0;
}
```

2. Contracts require the use of the keyword `do` (or `body`, see [DIP1003](https://github.com/dlang/DIPs/blob/master/DIPs/DIP1003.md)) before the function body. Any way of writing them that eliminated this need would be an improvement.

3. `in` and `out` contracts in D require explicitly writing `assert` statements, or some equivalent, in their code block in order to perform checks. But because of the nature of contracts, `assert` can be assumed in the vast majority of cases, and it is tedious to have to write it specifically. For the common case, the mere presence of `in` or `out` is sufficient. For those programmers who require the use of other forms of checking, the existing syntax will remain unchanged. 

Experience demonstrates, as in the case of `=>` lambdas, that a small change in syntax can dramatically affect the usage of a language feature. By adding a new syntax for contracts, a similar increase in their usage can be expected.

Furthermore, because the existing `invariant` grammar is so similar to `in` and `out` contract grammar, any improvements to `in` and `out` contracts can be transferred easily to invariants as well. Consistency across language constructs is desirable.

## Description

Take the grammar for existing `assert` parameters, and call it ContractParameters. Copy this grammar and use it in all the new syntaxes.

Introduce two new `in` and `out` contract syntaxes, called InExpression and OutExpression.

An InExpression is `in`, followed by parentheses containing ContractParameters, exactly mimicking an `assert` call.

The recommended OutExpression syntax is `out`, followed by parentheses containing an optional return identifier, a manditory semicolon, and ContractParameters (see Analysis below).

For example:
```d
int fun(ref int a, int b)
   in(a > 0)
   in(b >= 0, "b cannot be negative!")
   out(r; r > 0, "return must be positive")
   out(; a != 0)
{
   // function body
}
```
InExpressions and OutExpressions are considered the last part of the function signature. Multiple such expressions are permitted. The existing grammar for the function body, which includes ordinary bracketed `in` and `out` contracts, is unchanged. Therefore no `do` or `body` keyword is required after a new InExpression or OutExpression, unless it is also followed by a bracketed `in` or `out` contract. In the case of body-less virtual interface functions, a semicolon is required if there is no bracketed contract:
```d
interface I {
   int fun(int a) in(a > 0); 
}
```
A new type of `invariant` is introduced, again mimicking an `assert` call:
```d
class C {
   int data;
   invariant(data != 0, "cannot be 0");
}
```
By default, all the new contract expressions are treated simply as `assert` calls. This DIP does not prohibit other types of checking, but `assert` is expected to be the norm. In existing D, therefore, the above contracts would have to be written as, and would lower to:

```d
int fun(ref int a, int b)
in {
   assert(a > 0);
   assert(b >= 0, "b cannot be negative!");
}
out(r) {
   assert(r > 0, "return must be positive");
   assert(a != 0);
}
do {
   // function body
}

interface I {
   int fun(int a)
   in {
      assert(a > 0);
   } 
}

class C {
   int data;
   invariant {
      assert(data != 0, "data cannot be 0");
   }
}
```

## Analysis

The benefits of the new syntax are obvious. By removing the syntactic noise from the grammar, an increase in the usage of contracts can be expected.

A drawback of this conciseness is that a small usability gap will open up between those who find the built-in `assert` functionality adequate to their needs, and those who, for whatever reason, must use some other system of checking. The existing `in` and `out` contract syntax is still available for use by the latter. But the language now more strongly promotes the implicit `assert` functionality. The incentive is increased, therefore, to improve `assert` so that it serves as many people as possible.

### Analysis of the new `out` syntax

The most desirable syntax for the new `out` expression, i.e. a single parenthesized set of `assert` arguments,  is ambiguous with the existing `out` contract grammar. The presence of nothing but a single identifier inside the parentheses could be interpreted as either syntax. None of the proposed solutions is perfect.

1. The most straightforward solution is for the new grammar to require the condition to contain additional tokens when checking a non-return identifier. So instead of `out(i)`, for example, one must write `out(i != 0)`, and ambiguity is avoided. Any programmer who forgets to write those extra tokens will be met with a parsing error eventually, if not immediately.
```d
int fun(ref int i)
  out(i != 0) // okay
  out(i) // whoops! ambiguous with existing contract syntax
{
   // out contract, not function body
}
... // parsing error inevitable
```
The compiler can possibly use extra state logic to detect this particular error and assist the programmer with a helpful error message, "`out` contract expressions require more than just a single identifier", when the parsing finally fails.

But it is probably preferable to avoid this ambiguity altogether. Any proposed solution to the ambiguous `out` syntax must accomodate declaring the return identifier regardless. And all such methods of declaring return identifiers have the added advantage of parsing unambiguously with the existing syntax. Thus, simply by omitting the return identifier, they can be used unambiguously to check other variables as well, and are therefore recommended over option 1. Two possibilities of this type follow.

2. Use two sets of parentheses. This resembles the way the existing `out` contract syntax declares the return identifier. It also resembles the syntax of template function declarations.
```d
int fun(ref int i)
  out(r)(r > 0)
  out()(i > 0) // unambiguous parsing of a non-return identifier
{
  ...
}
```
The two parentheses style is slightly more verbose than the semicolon style which follows.

3. The semicolon style has already been introduced:
```d
int fun(ref int i)
  out(r; r > 0)
  out(; i > 0) // unambiguous parsing of a non-return identifier
{
  ...
}
```
It is the recommended option for this DIP because, while competitive with option 2,
* it is more concise, requiring only `;` to separate the return identifier
* the semantics of the new `out` expression resemble the semantics of `for` and `foreach`
* it is attractive for all new constructs to require the use of exactly one set of parentheses 

Note that existing `foreach` statements do not allow omitting the initial identifier — whereas the proposed `out` syntax does. If total consistency is desired, either both can be forced to declare an identifier that isn't used — or `foreach` can be changed, to allow omitting the identifier from it as well.

## Grammar
The grammar for `assert` parameters is redefined as ContractParameters, which is used in all the new constructs. Existing `in`, and `out` statements remain as they are. `invariant` statements are amended with the new variation. The `foreach` version of the new syntax is described here. New grammar follows:
```
ContractParameters:
    AssignExpression ,opt
    AssignExpression , AssignExpression ,opt

AssertExpression(redefined):
    assert ( ContractParameters )

InExpression:
    in ( ContractParameters )

OutExpression:
    out ( ; ContractParameters )
    out ( Identifier ; ContractParameters )

Invariant(redefined):
    invariant ( ) BlockStatement
    invariant BlockStatement
    invariant ( ContractParameters ) ;

FuncDeclaratorSuffix(redefined):
    Parameters MemberFunctionAttributes,opt ContractExpressions,opt
    TemplateParameters Parameters MemberFunctionAttributes,opt Constraint,opt ContractExpressions,opt

ContractExpressions:
    ContractExpression
    ContractExpression ContractExpressions

ContractExpression:
    InExpression
    OutExpression
```
## Code Breakage

None of the proposed syntax is currently compilable. Therefore no code is expected to break.

## Acknowledgements

This DIP was based upon an idea initially proposed by H.S. Teoh. It incoporates ideas from Moritz Maxeiner, Solomon E, Timon Gehr, and the DIP author.

## Copyright & License

Copyright (c) 2017 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/gjtsfysvtyxcfcmuutez@forum.dlang.org)

[Preliminary Review Round 2](http://forum.dlang.org/thread/luhdbjnsmfomtgpydser@forum.dlang.org)
